buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url 'https://oss.jfrog.org/oss-snapshot-local/'
        }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'antlr'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    antlr "org.antlr:antlr4:4.5.1"

    compile project(':stdlib')

    compile "org.antlr:antlr4-runtime:4.5.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.apache.commons:commons-collections4:4.1'

    testCompile project(':stdlib')

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M3'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M3'
}

junitPlatform {
    platformVersion '1.0.0-M3'
    filters {
        engines {
            include 'junit-jupiter'
        }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'co.kenrg.kagelang']
    outputDirectory = new File("generated-src/antlr/main/co/kenrg/kagelang".toString())
}
compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main/'

        compileClasspath += sourceSets.main.compileClasspath
    }

    test {
        compileClasspath += sourceSets.generated.output
    }

    main {
        compileClasspath += sourceSets.generated.output
    }
}
compileJava.source sourceSets.generated.java, sourceSets.main.java
compileKotlin.source sourceSets.generated.java, sourceSets.main.java, sourceSets.main.kotlin

clean {
    delete "generated-src"
}

idea {
    module {
        sourceDirs += file("generated-src/antlr/main")
    }
}
